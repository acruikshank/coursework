<html>
<head>
  <title>Paralax 2</title>
  <meta name="viewport" content="width=device-width, initial-scale=1">
  <style>
  body {
    background: #000;
    position: relative;
  }

  .parallax {
    perspective: 1px;
    position: relative;
    min-height: 100vh;
    overflow-x: hidden;
    overflow-y: auto;
    z-index: 0;
  }
  .parallax__layer {
    /*position: absolute;*/
    top: 0;
    right: 0;
    bottom: 0;
    left: 0;
  }
  .parallax__layer--base {
    padding: 60vh 0;
    transform: translateZ(0);
  }

  .parallax__layer--base2 {
    padding: 150vh 0;
    transform: translateZ(0);
  }
  .parallax__layer--back img {
    width: 100vw;
  }

  .parallax__layer--back {
    padding: 0 0 100vh;
    transform: translateZ(-.25px) scale(1.25);
    min-height: 50vh;
  }

  img.logo {
    width: 10vw;
    margin: 10vh auto;
    z-index: 1;
    position: relative;
  }

  .parallax__inner {
    background: rgb(38,38,38);
    position: relative;
    color: white;
    line-height: 40vh;
    font-size: 10vh;
    text-align: center;
    font-family: andale mono;
  }

  .canvas_container {
    position: absolute;
    top: 0;
    left: 0;
    width: 100%;
    height: 100%;
    z-index: 1;
  }

  canvas {
    position: fixed;
    top: 0;
    left: 0;
    width: 100%;
    height: 100%;
  }

  * {
    margin: 0;
    padding: 0;
  }
  </style>
    <script src="sketchbook-util.js" type="text/javascript"></script>

    <script id="dot" type="x-shader/x-fragment">
      precision mediump float;

      void main(void) {
          if ((gl_PointCoord.s > .1 && gl_PointCoord.s < .9)
            && (gl_PointCoord.t > .1 && gl_PointCoord.t < .9)) {
            discard;
          }
          gl_FragColor = vec4(1.0, 1.0, 1.0, 0.5);
      }
    </script>

    <script id="grid" type="x-shader/x-vertex">
      precision mediump float;

      attribute vec4 vertexPosition;

      uniform sampler2D state;
      uniform float yFactor;
      uniform float xFactor;
      uniform float freq;

      void main(void) {
        vec4 tex = texture2D(state,vertexPosition.xy * freq);
        gl_Position =  vec4(xFactor*tex.x, yFactor*tex.y, 0.0, 1.0);
        gl_PointSize = 25.0;
      }
    </script>

    <script id="computePhysics" type="x-shader/x-fragment">
      precision mediump float;

      uniform sampler2D state;
      uniform sampler2D origin;

      uniform float freq;
      uniform float dt;
      uniform float theta;

      float attraction(vec2 position) {
        return 90.0 * pow(.5*(snoise(vec3(3.0*position,theta))+1.0), 8.0);
      }

      void main(void) {
        vec4 tex = texture2D(state, gl_FragCoord.st * freq);
        vec2 position = tex.xy;
        vec2 velocity = tex.zw;
        vec2 lastVelocity = velocity;

        // float edge = ((i + (jOdd?2:0)) % 3) ? 1 : 0
        float iOdd = mod(gl_FragCoord.s - .5, 2.0);
        float jOdd = mod(gl_FragCoord.t - .5, 2.0);
        float centerOffset = mod(gl_FragCoord.s - .5 + jOdd * 2.0, 3.0);

        vec2 force = vec2(0.0);
        if (centerOffset > 0.0) {

          vec2 center1Index = vec2(gl_FragCoord.s + 2.0*(centerOffset - 1.5), gl_FragCoord.t)*freq;
          if (center1Index.x >= 0.0 && center1Index.x <= 1.0) {
            vec4 center1 = texture2D(state, center1Index);
            force += attraction(center1Index) * (center1.xy - position);
          }

          vec2 center2Index = vec2(gl_FragCoord.s + (1.0 - centerOffset + jOdd), gl_FragCoord.t+1.0)*freq;
          if (center2Index.y >= 0.0 && center2Index.y <= 1.0) {
            vec4 center2 = texture2D(state, center2Index);
            force += attraction(center2Index) * (center2.xy - position);
          }

          vec2 center3Index = vec2(gl_FragCoord.s + (1.0 - centerOffset + jOdd), gl_FragCoord.t-1.0)*freq;
          if (center3Index.y >= 0.0 && center3Index.y <= 1.0) {
            vec4 center3 = texture2D(state, center3Index);
            force += attraction(center3Index) * (center3.xy - position);
          }
          velocity += 0.1 * force;
        } else {
          vec2 index = gl_FragCoord.st * freq;
          if (index.s >= freq)
            force += texture2D(state, index.st + vec2(-freq,0.0)).xy - position;
          if (index.s <= 1.0-freq)
            force += texture2D(state, index.st + vec2(freq,0.0)).xy - position;
          if (index.t >= freq)
            force += texture2D(state, index.st + vec2(0.0,-freq)).xy - position;
          if (index.t <= 1.0-freq)
            force += texture2D(state, index.st + vec2(0.0,freq)).xy - position;

          if (jOdd > .5) {
            if (jOdd < 1.0 && index.s <= 1.0-freq && index.t >= freq)
              force += texture2D(state, index.st + vec2(freq,-freq)).xy - position;
            if (jOdd < 1.0 && index.s <= 1.0-freq && index.t >= 1.0-freq)
              force += texture2D(state, index.st + vec2(freq,freq)).xy - position;
          } else {
            if (jOdd < 1.0 && index.s > freq && index.t >= freq)
              force += texture2D(state, index.st + vec2(-freq,-freq)).xy - position;
            if (jOdd < 1.0 && index.s > freq && index.t >= 1.0-freq)
              force += texture2D(state, index.st + vec2(-freq,freq)).xy - position;
          }

          force += 1.5 * (texture2D(origin, index.st).xy - position);

          velocity += 0.1 * force;
        }
        velocity *= .8;
        position += lastVelocity * dt;
        gl_FragColor = vec4(position, velocity);
      }
    </script>

    <script id="passthrough" type="x-shader/x-vertex">
      precision mediump float;
      attribute vec2 position;

      void main(void) {
        gl_Position = vec4(position, 0.0, 1.0);
      }
    </script>

    <script id="noise" type="x-shader/x-library">
      precision mediump float;

      vec3 mod289(vec3 x) {
        return x - floor(x * (1.0 / 289.0)) * 289.0;
      }

      vec4 mod289(vec4 x) {
        return x - floor(x * (1.0 / 289.0)) * 289.0;
      }

      vec4 permute(vec4 x) {
           return mod289(((x*34.0)+1.0)*x);
      }

      vec4 taylorInvSqrt(vec4 r)
      {
        return 1.79284291400159 - 0.85373472095314 * r;
      }

      float snoise(vec3 v) {
        const vec2  C = vec2(1.0/6.0, 1.0/3.0) ;
        const vec4  D = vec4(0.0, 0.5, 1.0, 2.0);

        // First corner
        vec3 i  = floor(v + dot(v, C.yyy) );
        vec3 x0 =   v - i + dot(i, C.xxx) ;

        // Other corners
        vec3 g = step(x0.yzx, x0.xyz);
        vec3 l = 1.0 - g;
        vec3 i1 = min( g.xyz, l.zxy );
        vec3 i2 = max( g.xyz, l.zxy );

        //   x0 = x0 - 0.0 + 0.0 * C.xxx;
        //   x1 = x0 - i1  + 1.0 * C.xxx;
        //   x2 = x0 - i2  + 2.0 * C.xxx;
        //   x3 = x0 - 1.0 + 3.0 * C.xxx;
        vec3 x1 = x0 - i1 + C.xxx;
        vec3 x2 = x0 - i2 + C.yyy; // 2.0*C.x = 1/3 = C.y
        vec3 x3 = x0 - D.yyy;      // -1.0+3.0*C.x = -0.5 = -D.y

        // Permutations
        i = mod289(i);
        vec4 p = permute( permute( permute(
                   i.z + vec4(0.0, i1.z, i2.z, 1.0 ))
                 + i.y + vec4(0.0, i1.y, i2.y, 1.0 ))
                 + i.x + vec4(0.0, i1.x, i2.x, 1.0 ));

        // Gradients: 7x7 points over a square, mapped onto an octahedron.
        // The ring size 17*17 = 289 is close to a multiple of 49 (49*6 = 294)
        float n_ = 0.142857142857; // 1.0/7.0
        vec3  ns = n_ * D.wyz - D.xzx;

        vec4 j = p - 49.0 * floor(p * ns.z * ns.z);  //  mod(p,7*7)

        vec4 x_ = floor(j * ns.z);
        vec4 y_ = floor(j - 7.0 * x_ );    // mod(j,N)

        vec4 x = x_ *ns.x + ns.yyyy;
        vec4 y = y_ *ns.x + ns.yyyy;
        vec4 h = 1.0 - abs(x) - abs(y);

        vec4 b0 = vec4( x.xy, y.xy );
        vec4 b1 = vec4( x.zw, y.zw );

        //vec4 s0 = vec4(lessThan(b0,0.0))*2.0 - 1.0;
        //vec4 s1 = vec4(lessThan(b1,0.0))*2.0 - 1.0;
        vec4 s0 = floor(b0)*2.0 + 1.0;
        vec4 s1 = floor(b1)*2.0 + 1.0;
        vec4 sh = -step(h, vec4(0.0));

        vec4 a0 = b0.xzyw + s0.xzyw*sh.xxyy ;
        vec4 a1 = b1.xzyw + s1.xzyw*sh.zzww ;

        vec3 p0 = vec3(a0.xy,h.x);
        vec3 p1 = vec3(a0.zw,h.y);
        vec3 p2 = vec3(a1.xy,h.z);
        vec3 p3 = vec3(a1.zw,h.w);

        //Normalise gradients
        vec4 norm = taylorInvSqrt(vec4(dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3)));
        p0 *= norm.x;
        p1 *= norm.y;
        p2 *= norm.z;
        p3 *= norm.w;

        // Mix final noise value
        vec4 m = max(0.6 - vec4(dot(x0,x0), dot(x1,x1), dot(x2,x2), dot(x3,x3)), 0.0);
        m = m * m;
        return 42.0 * dot( m*m, vec4( dot(p0,x0), dot(p1,x1),
                                      dot(p2,x2), dot(p3,x3) ) );
      }
    </script>
  </head>
  <body>
    <div class="canvas_container">
      <canvas id="c"></canvas>
    </div>
    <div class="parallax">
      <div class="parallax__layer parallax__layer--back">
        <img src="robot.jpg"/>
        <img src="miami.jpg"/>
        <img src="people.jpeg"/>
      </div>
      <div class="parallax__layer parallax__layer--base">
        <div class="parallax__inner">
          <img class="logo" src="branch_logo.png"/>
        </div>
      </div>
      <div class="parallax__layer parallax__layer--base2">
        <div class="parallax__inner">
          <img class="logo" src="branch_logo.png"/>
        </div>
      </div>
    </div>
  <script>
var canvas;
var gl;
var pointBuffer, stateBuffer, originBuffer;
var coordAttribute, maskAttribute;
var physicsShader, onscreenShader;
var theta = 0;
var dTheta = 0.01;
var PIXELS = 32;

/*
11 - C -     10, 20, 01, 21,     12, 22
21 - E -         30, 11,             32
31 - E -     30,         41,     32
12 - E -     11,     02,         13
22 - E - 11,             32, 13,
32 - C - 21, 31,     22, 42, 23, 33

00 = 32

 2 4 8 16 64 128  = 222
 8 16 128 = 152
 4 32 64 = 100
 4 16 64 = 84
 1 16 32 = 49
 1 2 8 16 32 64 = 123

 */


start()

function start() {
  canvas = document.getElementById("c");
  canvas.width = canvas.offsetWidth * (window.devicePixelRatio || 1);
  canvas.height = canvas.offsetHeight * (window.devicePixelRatio || 1);

  gl = canvas.getContext("experimental-webgl");

  if (gl) {
    gl.getExtension('OES_texture_float');

    // gl.clearColor(0,0,0, 1.0);  // Clear to black, fully opaque

    onscreenShader = SketchbookUtil.createProgram(gl, 'grid', 'dot');
    physicsShader = SketchbookUtil.createProgram(gl, 'passthrough', 'computePhysics', [], ['noise']);

    coordAttribute = gl.getAttribLocation(onscreenShader, "vertexPosition");
    gl.enableVertexAttribArray(coordAttribute);

    maskAttribute = gl.getAttribLocation(onscreenShader, "mask");

    for (var j=0,v=[]; j<PIXELS; j++) {
      for (var i=0; i<PIXELS; i++) {
        pushHexVert(v,i,j)
      }
    }

    var vertices = new Float32Array(v);

    pointBuffer = gl.createBuffer();
    gl.bindBuffer(gl.ARRAY_BUFFER, pointBuffer);
    gl.bufferData(gl.ARRAY_BUFFER, vertices, gl.STATIC_DRAW);

    var offset = (PIXELS-1)/2;
    var factor = .75*(PIXELS+1)/2;
    for (var i=0, state=[]; i<v.length; i+=3) state.push((v[i]-offset)/factor,(v[i+1]-offset)/factor,0.00,0.00)
    var data = new Float32Array(state)
    stateBuffer = ReBuffer(gl, 2, PIXELS, {type: gl.FLOAT, data:[data,data]});
    originBuffer = ReBuffer(gl, 1, PIXELS, {type: gl.FLOAT, data:[new Float32Array(state)]});

    // Set up to draw the scene periodically.
    render();
  }
}

function render() {
  gl.enable(gl.DEPTH_TEST);
  gl.depthFunc(gl.LESS);
  gl.disable(gl.BLEND);

  gl.useProgram(physicsShader);

  stateBuffer.bindNext(gl);
  stateBuffer.uniform(gl, physicsShader, 1, 'state', 0);
  originBuffer.uniform(gl, physicsShader, 1, 'origin', 1);
  gl.uniform1f( gl.getUniformLocation(physicsShader, "dt"), 0.005);
  gl.uniform1f( gl.getUniformLocation(physicsShader, "freq"), 1/PIXELS);
  gl.uniform1f( gl.getUniformLocation(physicsShader, "theta"), theta);

  SketchbookUtil.drawFlatBackground(gl, physicsShader)

  gl.viewport(0, 0, canvas.width, canvas.height);
  gl.clear(gl.COLOR_BUFFER_BIT | gl.DEPTH_BUFFER_BIT);

  gl.disable(gl.DEPTH_TEST);
  gl.blendFunc(gl.SRC_ALPHA, gl.ONE);
  gl.enable(gl.BLEND);

  gl.useProgram(onscreenShader);

  stateBuffer.uniform(gl, onscreenShader, 1, 'state', 1);

  gl.uniform1f( gl.getUniformLocation(onscreenShader, "xFactor"), canvas.height / canvas.width);
  gl.uniform1f( gl.getUniformLocation(onscreenShader, "yFactor"), 1.0);
  gl.uniform1f( gl.getUniformLocation(onscreenShader, "freq"), 1/PIXELS);

  gl.bindBuffer(gl.ARRAY_BUFFER, pointBuffer);
  gl.vertexAttribPointer(coordAttribute, 3, gl.FLOAT, false, 0, 0);

  gl.drawArrays(gl.POINTS, 0, PIXELS*PIXELS);

  theta += dTheta;

  requestAnimationFrame(render);
}

function pushHexVert(vertices,i,j) {
  var iOdd = i&1, jOdd = j&1
  vertices.push(i + (jOdd?.5:0))
  vertices.push(j)
  var edge = ((i + (jOdd?2:0)) % 3) ? 1 : 0
  vertices.push(edge)
}

canvas.addEventListener('scroll', function(e) { console.log(e) })

  </script>
</body>
</html>
